ext {
  rootPackage = "com.glung.android"
  outputsDir = "$project.buildDir/outputs/apk"
  packageName = "${rootPackage}-app"
}

apply plugin: 'com.android.application'

android {
  compileSdkVersion rootProject.ext.androidCompileSdkVersion
  buildToolsVersion rootProject.ext.androidBuildToolsVersion

  defaultConfig {
    minSdkVersion 14
    versionCode 1     // auto-increment (?)
    versionName "1.0" // auto-increment (?)
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    targetSdkVersion rootProject.ext.androidTargetSdkVersion
    applicationId project.ext.rootPackage
  }

  buildTypes {
    release {
      minifyEnabled true
      applicationVariants.all { variant ->
        renameAPK(variant, "release")
      }
    }
    debug {
      minifyEnabled false
    }
  }
}

def renameAPK(variant, postfix) {
  variant.outputs.each { output ->
    def folder = project.ext.outputsDir
    def fileName = "${project.ext.packageName}-${postfix}.apk"
    output.outputFile = new File(folder, fileName)
  }
}

dependencies {
  compile 'com.android.support:appcompat-v7:25.3.0'

  testCompile 'junit:junit:4.12'

  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  androidTestCompile 'com.squareup.spoon:spoon-client:1.7.1'
}
